#!/home/toshok/src/expect-5.42/expect -f

set mono mono
set mdb "/home/toshok/src/mono/debugger/wrapper/mdb.exe"
set max_repeat 10
set timeout 20
set mdb_prompt "\[\(mdb\)\]"
set verbose 2
set file "/usr/lib/mono/1.0/mcs.exe"
set args "/d:NET_1_1 /d:ONLY_1_1 -g /target:exe /out:mcs.exe cs-parser.cs @mcs.exe.sources"
set compilation_succeeded 0
set num_steps 0

proc mdb_start {} {
    global mdb_prompt
    global spawn_id
    global file
    global args
    global mono
    global mdb

    set res [spawn -ignore HUP $mono $mdb]
    if { $res < 0 || $res == "" } {
	fail "Spawning $mdb failed."
    }

    expect {
	-re "Mono Debugger.*$mdb_prompt $" {
	    send_user "Mono Debugger initialized\n"
	}
	timeout {
	    fail "(timeout) Mono Debugger not starting"
	}
    }
}

proc mdb_send_step { } {
    global num_steps

    set num_steps [expr $num_steps + 1]

    set v [expr int( rand() * 2)]

    switch $v {
	1 { mdb_send "next" }
	default { mdb_send "step" }
    }
}

proc mdb_step_until_finished { } {
    global mdb_prompt
    global compilation_succeeded

    expect {
	-re "^Compilation succeeded" {
	    set compilation_succeeded 1
	    exp_continue
	}
	-re "No program loaded.\[\r\n\]+" {
	    send_user "FAIL: no child to debug\n"
	    exit 1
	}
	-re "No program to debug.\[\r\n\]+" {
	    return
	}
	-re "Process already started.*$mdb_prompt $" {
	    mdb_send_step
	    exp_continue
	}
	-re "Process @\[0-9\]+ stopped.*$mdb_prompt $" {
	    mdb_send_step
	    exp_continue
	}
	-re "^ \[0-9\]+\[^\r\n\]+\[\r\n\]+" {
	    exp_continue
	}
	-re "^ERROR:(\[^\n\r\]+)" {
	    set message $expect_out(1,string)
	    send_user "FAIL: Could not step: $message\n"
	    exit 1
	}
	timeout {
	    send_user "timeout waiting for step prompt\n"
	}
    }

}

proc mdb_send {command} {
    send $command\r\n
    expect -re "$command\[\r\n\]+" {}
    return 0
}

proc mdb_style {style} {
    global mdb_prompt;

    mdb_send "style $style"
    expect {
	-re "Current style interface:.*$mdb_prompt $" {
	    return 0
	}
	-re ".*$mdb_prompt $" {
	    send_user "Error setting style\n"
	    exit 1
	}
	timeout {
	    send_user "timeout setting style\n"
	    exit 1
	}
    }
}

mdb_start
#mdb_style "regress"
mdb_send "start $file $args"
mdb_send "print args"
mdb_send "run"
mdb_step_until_finished
if {$compilation_succeeded == 1} {
    send_user "PASS: Compilation succeeded using $num_steps steps\n"
} else {
    send_user "FAIL: Compilation failed using $num_steps steps\n"
}