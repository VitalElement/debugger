%{
using System.Text;
using System.IO;
using System.Collections;
using System;

namespace Mono.Debugger.Frontend.CSharp
{
	internal class ExpressionParser : IExpressionParser
	{
		ScriptingContext current_context;
		MyTextReader reader;
		Tokenizer lexer;

		protected bool yacc_verbose_flag = false;

		public bool Verbose {
			set {
				yacc_verbose_flag = value;
			}

			get {
				return yacc_verbose_flag;
			}
		}

%}

%token QUIT
%token EOF
%token NONE	// This token is never returned by our lexer
%token ERROR	// This is used not by the parser, but by the tokenizer.
		// do not remove.

%token IDENTIFIER
%token INTEGER
%token FLOAT
%token NUMBER
%token STRING
%token HASH
%token AT
%token PERCENT
%token DOLLAR
%token DOT
%token DOTDOT
%token NOT
%token COMMA
%token EQUAL
%token EQUALEQUAL
%token NOTEQUAL
%token STAR
%token PLUS
%token MINUS
%token DIV
%token OPAREN
%token CPAREN
%token OBRACKET
%token CBRACKET
%token LT
%token GT
%token LE
%token GE
%token AND
%token OR
%token COLON
%token QUESTION
%token AMPERSAND
%token ARROW

%token LENGTH
%token LOWER
%token UPPER

%token NEW
%token THIS
%token BASE
%token CATCH
%token TRUE
%token FALSE
%token NULL

%start parse_expression
%%

parse_expression
	: primary_expression
	  {
		return $1;
	  }
	;

primary_expression
	: expression
	| expression EQUAL expression
	  {
		$$ = new AssignmentExpression ((Expression) $1, (Expression) $3);
	  }
	;	

constant
	: TRUE
	  {
		$$ = new BoolExpression (true);
	  }
	| FALSE
	  {
		$$ = new BoolExpression (false);
	  }
	| NUMBER
	  {
		$$ = new NumberExpression ((long) $1);
	  }
	| INTEGER
	  {
		$$ = new NumberExpression ((int) $1);
	  }
	| FLOAT
	  {
		$$ = new NumberExpression ((float) $1);
	  }
	| STRING
	  {
		$$ = new StringExpression ((string) $1);
	  }

expression
	: constant
	| THIS
	  {
		$$ = new ThisExpression ();
	  }
	| CATCH
	  {
		$$ = new CatchExpression ();
	  }
	| BASE DOTDOT IDENTIFIER
	  {
		$$ = new MemberAccessExpression (new BaseExpression (), "." + ((string) $3));
	  }
	| BASE DOT IDENTIFIER
	  {
		$$ = new MemberAccessExpression (new BaseExpression (), (string) $3);
	  }
	| variable_or_type_name
	| PERCENT IDENTIFIER
	  {
		$$ = new RegisterExpression ((string) $2, 0);
	  }
	| STAR expression
	  {
		$$ = new PointerDereferenceExpression ((Expression) $2, false);
	  }
	| AMPERSAND expression
	  {
		$$ = new AddressOfExpression ((Expression) $2);
	  }
	| expression OBRACKET expression CBRACKET
	  {
		$$ = new ArrayAccessExpression ((Expression) $1, (Expression) $3);
	  }
	| expression OPAREN argument_list CPAREN
	  {
		$$ = new InvocationExpression ((Expression) $1, ((Expression []) $3));
	  }
	| NEW variable_or_type_name OPAREN argument_list CPAREN
	  {
		$$ = new NewExpression ((Expression) $2, ((Expression []) $4));
	  }
	| OPAREN variable_or_type_name CPAREN expression
	  {
		$$ = new CastExpression ((Expression) $2, (Expression) $4);
	  }
	| expression QUESTION expression COLON expression
	  {
		$$ = new ConditionalExpression ((Expression)$1, (Expression)$3, (Expression)$5);
	  }
	| OPAREN expression CPAREN
	  {
		$$ = $2;
	  }
	;

argument_list
	: /* empty */
	  {
		$$ = new Expression [0];
	  }
	| argument_list_0
	  {
		Expression[] args = new Expression [((ArrayList) $1).Count];
		((ArrayList) $1).CopyTo (args, 0);

		$$ = args;
	  }
	;

argument_list_0
	: expression
	  {
		ArrayList args = new ArrayList ();
		args.Add ($1);

		$$ = args;
	  }
	| argument_list_0 COMMA expression
	  {
		ArrayList args = (ArrayList) $1;
		args.Add ($3);

		$$ = args;
	  }
	;

variable_or_type_name
	: IDENTIFIER
	  {
		$$ = new SimpleNameExpression ((string) $1);
	  }
	| expression DOT IDENTIFIER
	  { 
		$$ = new MemberAccessExpression ((Expression) $1, (string) $3);
	  }
	| expression DOTDOT IDENTIFIER
	  { 
		$$ = new MemberAccessExpression ((Expression) $1, "." + (string) $3);
	  }
	| expression ARROW IDENTIFIER
	  {
		Expression expr = new PointerDereferenceExpression ((Expression) $1, true);
		$$ = new MemberAccessExpression (expr, (string) $3);
	  }
	;

%%

public ExpressionParser (ScriptingContext context, string name)
{
	this.reader = new MyTextReader ();
	this.current_context = context;

	lexer = new Tokenizer (reader);
	lexer.Name = name;
}

public Expression Parse (string text)
{
	try {
		reader.Text = text;
		lexer.restart ();
		if (yacc_verbose_flag)
			return (Expression) yyparse (lexer, new yydebug.yyDebugSimple ());
		else
			return (Expression) yyparse (lexer);
	} catch (Exception e){
		// Please do not remove this, it is used during debugging
		// of the grammar
		//
		current_context.Error (lexer.Location + "  : Parsing error ");
		current_context.Error (e.ToString ());
		return null;
	}
}

/* end end end */
}
